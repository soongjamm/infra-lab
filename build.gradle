plugins {
    id 'io.freefair.lombok' version '6.0.0-m2' apply false
    id 'org.springframework.boot' version '2.7.18' apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
    id 'com.google.cloud.tools.jib' version '3.1.2' apply false
}

group = 'com.soongjamm'
version = '0.0.1-SNAPSHOT'


def javaProjects = [project(':jib'), project(':spring-batch'), project(':jmh-benchmark'), project(':spring-data-redis'), project(':spring-autoconfig')]
def springBootProjects = [project(':jib'), project(':spring-batch'), project(':spring-data-redis'), project(':spring-autoconfig')]

configure(javaProjects) {
    apply plugin: 'java'
    apply plugin: 'io.freefair.lombok'

    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = '11'
        sourceCompatibility = '11'
        options.encoding = 'UTF-8'
    }

    dependencies {
        if (!springBootProjects.contains(project)) {
            testImplementation(platform('org.junit:junit-bom:5.7.1'))
            testImplementation 'org.junit.jupiter:junit-jupiter'
            testImplementation 'org.assertj:assertj-core:3.20.2'
            testImplementation 'org.mockito:mockito-junit-jupiter:3.12.4'
        }
    }
}

configure(springBootProjects) {
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }
}
